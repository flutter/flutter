# Copyright (c) 2013 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

# This header file defines the "sysroot" variable which is the absolute path
# of the sysroot. If no sysroot applies, the variable will be an empty string.

import("//build/toolchain/goma.gni")

declare_args() {
  # The absolute path of the sysroot that is applied when compiling using
  # the target toolchain.
  target_sysroot = ""

  # Whether to use the default sysroot when building for Linux, if an explicit
  # sysroot isn't set.
  use_default_linux_sysroot = true

  # The absolute path to the Xcode toolchain. This is used to look for headers
  # that usually ship with the toolchain like c++/v1.
  xcode_toolchain = ""
}

if (current_toolchain == default_toolchain && target_sysroot != "") {
  sysroot = target_sysroot
} else if (is_android) {
  import("//build/config/android/config.gni")
  sysroot = rebase_path("$android_ndk_root/sysroot")
} else if (is_linux && !is_chromeos) {
  if (current_cpu == "mipsel") {
    sysroot = rebase_path("//mipsel-sysroot/sysroot")
  } else if (use_default_linux_sysroot && !is_fuchsia) {
    if (current_cpu == "x64") {
      sysroot = rebase_path("//build/linux/debian_sid_amd64-sysroot")
    } else {
      sysroot = rebase_path("//build/linux/debian_sid_arm64-sysroot")
    }
    assert(
        exec_script("//build/dir_exists.py", [ sysroot ], "string") == "True",
        "Missing sysroot ($sysroot). To fix, run: build/linux/sysroot_scripts/install-sysroot.py --arch=$current_cpu")
  } else {
    sysroot = ""
  }
} else if (is_mac) {
  import("//build/config/mac/mac_sdk.gni")
  sysroot = mac_sdk_path
} else if (is_ios) {
  import("//build/config/ios/ios_sdk.gni")
  sysroot = ios_sdk_path
} else {
  sysroot = ""
}

if ((is_mac || is_ios) && xcode_toolchain == "") {
  xcode_toolchain_args = []
  if (use_goma && create_xcode_symlinks) {
    xcode_toolchain_args += [
      "--symlink",
      rebase_path(root_gen_dir),
    ]
  }
  xcode_toolchain = exec_script("//build/config/mac/xcode_toolchain.py",
                                xcode_toolchain_args,
                                "trim string",
                                [])
}
