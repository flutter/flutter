# Copyright 2016 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

assert(is_mac || is_ios)

group("darwin") {
  if (is_mac) {
    deps = [
      "desktop:shell_standalone",
    ]
    if (!is_fuchsia_host) {
      deps += [
        "desktop:shell_application_bundle",
      ]
    }
  } else if (is_ios) {
    deps = [
      "ios:flutter_framework",
    ]
  } else {
    assert(false, "Unknown darwin platform type.")
  }
}

source_set("flutter_channels") {
  set_sources_assignment_filter([])
  sources = [
    "common/buffer_conversions.h",
    "common/buffer_conversions.mm",
    "common/FlutterBinaryMessenger.h",
    "common/FlutterChannels.h",
    "common/FlutterCodecs.h",
    "common/FlutterChannels.mm",
    "common/FlutterCodecs.mm",
    "common/FlutterStandardCodec.mm",
    "common/FlutterStandardCodec_Internal.h",
  ]
  set_sources_assignment_filter(sources_assignment_filter)

  deps = [
    "$flutter_root/common",
    "$flutter_root/flow",
    "$flutter_root/fml",
    "$flutter_root/runtime",
    "$flutter_root/shell/common",
    "$flutter_root/shell/gpu",
    "$flutter_root/shell/testing",
    "$flutter_root/sky/engine/wtf",
    "//garnet/public/lib/fxl",
    "//third_party/skia",
  ]

  public_configs = [
    "$flutter_root:config",
  ]
}

executable("flutter_channels_unittests") {
  testonly = true

  set_sources_assignment_filter([])
  sources = [
    "common/flutter_codecs_unittest.mm",
    "common/flutter_standard_codec_unittest.mm",
  ]
  set_sources_assignment_filter(sources_assignment_filter)

  deps = [
    ":flutter_channels",
    "//third_party/dart/runtime:libdart_jit",
    "$flutter_root/testing",
  ]
}
