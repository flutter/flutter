# Copyright 2016 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

assert(is_mac)

import("$flutter_root/shell/gpu/gpu.gni")

shell_gpu_configuration("darwin_desktop_gpu_configuration") {
  enable_software = false
  enable_vulkan = false
  enable_gl = true
}

source_set("mac_desktop_platform") {
  visibility = [ ":*" ]

  sources = [
    "flutter_application_delegate.h",
    "flutter_application_delegate.mm",
    "flutter_window.h",
    "flutter_window.mm",
    "main_mac.mm",
    "platform_view_mac.h",
    "platform_view_mac.mm",
    "vsync_waiter_mac.cc",
    "vsync_waiter_mac.h",
  ]

  deps = [
    ":darwin_desktop_gpu_configuration",
    "$flutter_root/common",
    "$flutter_root/fml",
    "$flutter_root/lib/snapshot",
    "$flutter_root/shell/common",
    "$flutter_root/shell/platform/darwin/common",
    "$flutter_root/synchronization",
    "//third_party/dart/runtime:libdart_jit",
    "//third_party/skia",
    "//third_party/skia:gpu",
  ]

  public_configs = [ "$flutter_root:config" ]
}

if (!is_fuchsia_host) {
  import("//build/config/mac/rules.gni")

  resource_copy_mac("mac_desktop_resources") {
    app_name = "FlutterTester"
    resources = [ "//third_party/icu/flutter/icudtl.dat" ]
    bundle_directory = "."
  }

  mac_app("shell_application_bundle") {
    app_name = "FlutterTester"
    info_plist = "Info.plist"

    deps = [
      ":mac_desktop_platform",
      ":mac_desktop_resources",
    ]
  }
}
