# Copyright 2019 The Flutter Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/toolchain/wasm.gni")

template("skwasm_variant") {
  wasm_lib(target_name) {
    public_configs = [ "//flutter:config" ]

    sources = [
      "canvas.cpp",
      "canvas_text.h",
      "contour_measure.cpp",
      "data.cpp",
      "export.h",
      "filters.cpp",
      "fonts.cpp",
      "helpers.h",
      "image.cpp",
      "images.h",
      "live_objects.cpp",
      "live_objects.h",
      "paint.cpp",
      "path.cpp",
      "picture.cpp",
      "render_context.h",
      "shaders.cpp",
      "skwasm_support.h",
      "string.cpp",
      "surface.cpp",
      "text/line_metrics.cpp",
      "text/paragraph.cpp",
      "text/paragraph_builder.cpp",
      "text/paragraph_style.cpp",
      "text/strut_style.cpp",
      "text/text_style.cpp",
      "text/text_types.h",
      "vertices.cpp",
      "wrappers.h",
    ]

    cflags = [ "-mreference-types" ]

    ldflags = [
      "-std=c++20",
      "-lGL",
      "-sUSE_WEBGL2=1",
      "-sMAX_WEBGL_VERSION=2",
      "-sOFFSCREENCANVAS_SUPPORT",
      "-sALLOW_MEMORY_GROWTH",
      "-sALLOW_TABLE_GROWTH",
      "-lexports.js",
      "-sEXPORTED_FUNCTIONS=[stackAlloc]",
      "-sEXPORTED_RUNTIME_METHODS=[addFunction,wasmExports,wasmMemory,stackAlloc]",
      "-sINCOMING_MODULE_JS_API=[instantiateWasm,locateFile,noExitRuntime,mainScriptUrlOrBlob,wasmMemory,wasm,skwasmSingleThreaded]",
      "-sUSE_ES6_IMPORT_META=0",
      "--js-library",
      rebase_path("library_skwasm_support.js"),
    ]

    inputs = [ rebase_path("library_skwasm_support.js") ]
    if (invoker.is_heavy) {
      sources += [
        "animated_image.cpp",
        "text/paragraph_builder_builtin_icu.cpp",
      ]
    } else {
      sources += [
        "animated_image_stub.cpp",
        "text/paragraph_builder_client_icu.cpp",
      ]
    }

    if (invoker.use_impeller) {
      sources += [
        "canvas_text_impeller.cpp",
        "images_impeller.cpp",
        "render_context_impeller.cpp",
        "shaders_impeller.cpp",
      ]
    } else {
      sources += [
        "canvas_text_skia.cpp",
        "images_skia.cpp",
        "render_context_skia.cpp",
        "shaders_skia.cpp",
      ]
    }

    if (is_debug) {
      ldflags += [
        "-sASSERTIONS=1",
        "-sGL_ASSERTIONS=1",
      ]
    } else {
      ldflags += [ "--closure=1" ]
    }

    deps = [
      "//flutter/display_list",
      "//flutter/skia/modules/skparagraph",
      "//flutter/skia/modules/skunicode",
    ]

    if (invoker.use_impeller) {
      deps += [ "//flutter/impeller" ]
    } else {
      deps += [ "//flutter/skia" ]
    }
  }
}

skwasm_variant("skwasm") {
  is_heavy = false
  use_impeller = false
}

skwasm_variant("skwasm_heavy") {
  is_heavy = true
  use_impeller = false
}

skwasm_variant("wimp") {
  is_heavy = false
  use_impeller = true
}
