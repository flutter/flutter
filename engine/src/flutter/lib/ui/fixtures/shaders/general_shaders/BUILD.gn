# Copyright 2013 The Flutter Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/compiled_action.gni")
import("//flutter/impeller/tools/impeller.gni")
import("//flutter/testing/testing.gni")

if (enable_unittests) {
  test_shaders = [
    "blue_green_sampler.frag",
    "children_and_uniforms.frag",
    "double_sampler_swapped.frag",
    "double_sampler.frag",
    "filter_shader.frag",
    "functions.frag",
    "missing_size.frag",
    "missing_texture.frag",
    "no_builtin_redefinition.frag",
    "no_uniforms.frag",
    "simple.frag",
    "texture.frag",
    "uniform_arrays.frag",
    "uniform_ordering.frag",
    "uniforms_inserted.frag",
    "uniforms_reordered.frag",
    "uniforms_sorted.frag",
    "uniforms.frag",
    "uniforms_renamed.frag",
    "vec3_uniform.frag",
  ]

  group("general_shaders") {
    testonly = true
    deps = [ ":fixtures" ]
  }

  impellerc("compile_general_shaders") {
    mnemonic = "IMPELLERC_SKSL"
    shaders = test_shaders
    shader_target_flags = [ "--sksl" ]
    intermediates_subdir = "iplr"
    sl_file_extension = "iplr"
    iplr = true
  }

  impellerc("compile_sdf_shaders") {
    mnemonic = "IMPELLERC_SDF"
    shaders = [
      "circle_sdf.frag",
      "sdf.frag",
    ]
    shader_target_flags = [
      "--sksl",
      "--runtime-stage-metal",
      "--runtime-stage-gles",
      "--runtime-stage-vulkan",
    ]
    intermediates_subdir = "iplr"
    sl_file_extension = "iplr"
    iplr = true
  }

  test_fixtures("fixtures") {
    deps = [
      ":compile_general_shaders",
      ":compile_sdf_shaders",
    ]
    fixtures = get_target_outputs(":compile_general_shaders") +
               get_target_outputs(":compile_sdf_shaders")
    dest = "$root_gen_dir/flutter/lib/ui"
  }
}
