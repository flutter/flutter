# Copyright 2015 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//mojo/public/tools/bindings/mojom.gni")

group("vsync") {
  deps = []

  if (is_android || is_ios) {
    deps += [ ":vsync_lib" ]
  }
}

if (is_android) {
  import("//build/config/android/config.gni")
  import("//build/config/android/rules.gni")

  android_library("vsync_lib") {
    java_files = [ "src/org/domokit/vsync/VSyncProviderImpl.java" ]

    deps = [
      "//base:base_java",
      "//mojo/public/java:bindings",
      "//mojo/public/java:system",
      "//mojo/services/vsync/interfaces:interfaces_java",
    ]
  }
}

if (is_ios) {
  source_set("vsync_lib") {
    sources = [
      "ios/vsync_provider_impl.h",
      "ios/vsync_provider_impl.mm",
    ]
    deps = [
      "//base:base",
      "//mojo/public/cpp/application",
      "//mojo/services/vsync/interfaces",
    ]
  }
}
