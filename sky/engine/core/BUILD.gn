# Copyright 2014 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//flutter/common/config.gni")
import("//flutter/sky/engine/core/core.gni")

visibility = [
  "//flutter/sky/engine/*",
  "//flutter/sky/shell/*",
]

static_library("core") {
  output_name = "sky_core"

  deps = [
    "//flutter/assets",
    "//flutter/sky/engine/platform",
  ]

  if (flutter_runtime_mode != "release") {
    # Only include observatory assets in non-release modes.
    deps += [ "//dart/runtime/observatory:embedded_observatory_archive" ]
  }

  configs += [
    "//flutter/sky/engine:config",
    "//flutter/sky/engine:inside_blink",
  ]

  sources = sky_core_files

  public_deps = [
    "//flutter/common",
    "//flutter/flow",
    "//flutter/glue",
    "//flutter/sky/engine/wtf",
    "//garnet/public/lib/fxl",
    "//third_party/libpng",
    "//third_party/skia",
    "//third_party/zlib",
    "//topaz/lib/tonic",
  ]

  if (flutter_runtime_mode != "release") {
    public_deps += [ "//topaz/lib/tonic/debugger" ]
  }
}
