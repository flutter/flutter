# Copyright 2014 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/ui.gni")
if (is_android) {
  import("//build/config/android/config.gni")
  import("//build/config/android/rules.gni")
}

source_set("gl") {
  sources = [
    "gl_bindings.h",
    "gl_bindings_autogen_gl.cc",
    "gl_bindings_autogen_gl.h",
    "gl_bindings_skia_in_process.cc",
    "gl_bindings_skia_in_process.h",
    "gl_context.cc",
    "gl_context.h",
    "gl_context_stub.cc",
    "gl_context_stub.h",
    "gl_context_stub_with_extensions.cc",
    "gl_context_stub_with_extensions.h",
    "gl_enums.cc",
    "gl_enums.h",
    "gl_enums_implementation_autogen.h",
    "gl_export.h",
    "gl_gl_api_implementation.cc",
    "gl_gl_api_implementation.h",
    "gl_implementation.cc",
    "gl_implementation.h",
    "gl_share_group.cc",
    "gl_share_group.h",
    "gl_surface.cc",
    "gl_surface.h",
    "gl_surface_stub.cc",
    "gl_surface_stub.h",
    "gl_switches.cc",
    "gl_switches.h",
    "gl_version_info.cc",
    "gl_version_info.h",
    "gpu_timing.cc",
    "gpu_timing.h",
  ]

  public_deps = [
    "//base",
    "//third_party/mesa:mesa_headers",
    "//ui/gfx",
    "//ui/gfx/geometry",
  ]

  deps = [
    "//base/third_party/dynamic_annotations",
    "//skia",
  ]

  if (is_android || is_linux) {
    sources += [
      "gl_bindings.cc",
      "gl_bindings_autogen_osmesa.cc",
      "gl_bindings_autogen_osmesa.h",
      "gl_context_osmesa.cc",
      "gl_context_osmesa.h",
      "gl_implementation_osmesa.cc",
      "gl_implementation_osmesa.h",
      "gl_osmesa_api_implementation.cc",
      "gl_osmesa_api_implementation.h",
      "gl_state_restorer.cc",
      "gl_state_restorer.h",
      "gl_surface_osmesa.cc",
      "gl_surface_osmesa.h",
      "scoped_binders.cc",
      "scoped_binders.h",
      "scoped_make_current.cc",
      "scoped_make_current.h",
      "sync_control_vsync_provider.cc",
      "sync_control_vsync_provider.h",
    ]
  }
  if (is_linux) {
    deps += [ "//third_party/libevent" ]
  }
  if (use_glfw) {
    sources += [
      "gl_context_glfw.cc",
      "gl_context_glfw.h",
      "gl_implementation_glfw.cc",
      "gl_surface_glfw.cc",
      "gl_surface_glfw.h",
    ]

    deps += [ "//third_party/glfw" ]
  }
  if (use_x11) {
    sources += [
      "gl_context_osmesa_x11.cc",
      "gl_surface_osmesa_x11.cc",
      "gl_surface_osmesa_x11.h",
    ]

    configs += [
      "//build/config/linux:x11",
      "//build/config/linux:xcomposite",
      "//build/config/linux:xext",
    ]

    deps += [ "//ui/gfx/x" ]
  }
  if (is_android) {
    sources += [
      "android/gl_jni_registrar.cc",
      "android/gl_jni_registrar.h",
      "android/scoped_java_surface.cc",
      "android/scoped_java_surface.h",
      "android/surface_texture.cc",
      "android/surface_texture.h",
      "android/surface_texture_listener.cc",
      "android/surface_texture_listener.h",
      "egl_util.cc",
      "egl_util.h",
      "gl_bindings_autogen_egl.cc",
      "gl_bindings_autogen_egl.h",
      "gl_context_android.cc",
      "gl_context_egl.cc",
      "gl_context_egl.h",
      "gl_egl_api_implementation.cc",
      "gl_egl_api_implementation.h",
      "gl_implementation_android.cc",
      "gl_surface_android.cc",
      "gl_surface_egl.cc",
      "gl_surface_egl.h",
    ]

    defines = [
      "GL_GLEXT_PROTOTYPES",
      "EGL_EGLEXT_PROTOTYPES",
    ]

    libs = [ "android" ]

    deps += [ ":gl_jni_headers" ]
  }

  if (is_ios) {
    sources += [
      "gl_context_ios.h",
      "gl_context_ios.mm",
      "gl_implementation_ios.cc",
      "gl_surface_ios.h",
      "gl_surface_ios.mm",
    ]
  }

  if (is_mac) {
    sources += [
      "gl_context_cgl.cc",
      "gl_context_cgl.h",
      "gl_context_mac.h",
      "gl_context_mac.mm",
      "gl_implementation_mac.cc",
      "gl_surface_mac.h",
      "gl_surface_mac.mm",
      "gpu_switching_manager.cc",
      "gpu_switching_manager.h",
    ]
  }
}

if (is_android) {
  generate_jar_jni("surface_jni_headers") {
    jni_package = "ui/gl"
    classes = [ "android/view/Surface.class" ]
  }

  generate_jni("gl_jni_headers") {
    deps = [
      ":surface_jni_headers",
    ]
    sources = [
      "android/java/src/org/chromium/ui/gl/SurfaceTextureListener.java",
      "android/java/src/org/chromium/ui/gl/SurfaceTexturePlatformWrapper.java",
    ]
    jni_package = "ui/gl"
  }
}
